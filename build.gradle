apply plugin: "groovy"
apply plugin: 'maven-publish'
apply plugin: 'codenarc'

version = "1.0"
description = "Groovy language mixin"

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.codenarc:CodeNarc:0.21'
}

test {
    if (project.hasProperty('maxParallelForks'))
        maxParallelForks = project.maxParallelForks as int
    if (project.hasProperty('forkEvery'))
        forkEvery = project.forkEvery as int
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.hex0cter.groovy'
            artifactId 'mixin'
            version '1.0'

            from components.java
        }
    }
}

codenarc {
    ignoreFailures false
    configFile file('config/codenarc/codenarc.rules')

    maxPriority1Violations 0
    maxPriority2Violations 0
    maxPriority3Violations 0
}
